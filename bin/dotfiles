#!/usr/bin/env zsh
set -e

CURRENT_DIR="$PWD"
DOTHOME="$( cd "$( dirname "$0" )/.." && pwd )"

function run() {
  ensure_git
  get_dotfiles
  link_dotfiles
  run_init_scripts
  print_messages
}

# Directory to store backup
backup_dir="$DOTHOME/backups/$(date "+%Y_%m_%d-%H_%M_%S")/"

# Logging stuff.
function e_header()   { echo -e "\n\033[1m$@\033[0m"; }
function e_success()  { echo -e " \033[1;32m✔\033[0m  $@"; }
function e_error()    { echo -e " \033[1;31m✖\033[0m  $@"; }
function e_arrow()    { echo -e " \033[1;33m➜\033[0m  $@"; }

function ensure_git() {
  if [[ -z $commands[git] && "$OSTYPE" =~ ^darwin ]]; then
    if [[ -n $commands[brew] ]]; then
      e_header "Installing Homebrew..."
      ruby -e "$(curl -fsSL https://raw.github.com/mxcl/homebrew/go)"
    fi

    if [[ -n $commands[brew] ]]; then
      e_header "Updating Homebrew..."
      brew update
      e_header "Installing Git..."
      brew install git
    fi
  fi

  # If Git isn't installed by now, something exploded. 
  if [[ -z $commands[git] ]]; then
    e_error "Git should be installed. It isn't. Aborting."
    exit 1
  fi
}

function get_dotfiles() {
  e_header "Updating dotfiles..."
echo $DOTHOME
  cd $DOTHOME && git pull
  git submodule update --init --recursive --quiet
}

function link_dotfiles() {
  e_header "Linking files into home directory..."

  for file in $DOTHOME/home/.*; do
    local base="$(basename $file)"
    local dest="$HOME/$base"

    # Skip if link is the same.
    if [[ "$file" -ef "$dest" ]]; then
      e_success "$HOME/$base"
      continue
    fi

    # Back up file if it exists.
    if [[ -e "$dest" ]]; then
      e_arrow "Backing up $HOME/$base."
      inform_about_backup=1
      mkdir -p "$backup_dir"
      mv "$dest" "$backup_dir"
    fi

    ln -sf ${file#$HOME/} $HOME/
    e_success "$HOME/$base"
  done
}

function run_init_scripts() {
  e_header "Executing init scripts..."

  TRAPERR() {
    e_error $SOURCED_FILE
  }

  for file in $DOTHOME/init/*; do
    SOURCED_FILE="$(basename $file)"
    source $file
    e_success $SOURCED_FILE
  done
}

function print_messages() {
  if [[ $inform_about_backup == 1 ]]; then
    echo "\nBackups were moved to $HOME/${backup_dir#$HOME/}\n"
  fi

  if [[ $first_run == 1 ]]; then
    echo "\nInstallation complete! You can relogin now."
  else
    echo # it's visually better ;)
  fi
}

run

cd $CURRENT_DIR && exec zsh
